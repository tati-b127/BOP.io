#Prog VPMEditor_GetExchangeCompletionOnReference(iPLMIDSet :  #In PLMIDSet,oPLMIDSet :  #Out PLMIDSet) : VoidType
 { 
let PLMRouteSet1(PLMRouteSet)
let PLMRouteSet2(PLMRouteSet)
let PLMRouteSet3(PLMRouteSet)
let PLMRouteSet4(PLMRouteSet)
let rsXCADRepRepInst(PLMRouteSet)

let PLMIDSetRestricted(PLMIDSet)
let PLMIDSet1Restricted(PLMIDSet)
let PLMIDSet1(PLMIDSet)
let PLMIDSet2(PLMIDSet)
let PLMIDSetRep(PLMIDSet)

let idsXCADComposition(PLMIDSet)
let idsXCADNonPS(PLMIDSet)

PLMIDSetRestricted = Restrict(iPLMIDSet, "PRODUCTCFG/VPMReference")

/*                      */
/* Enrich Ref with XCAD */ 
/*                      */
idsXCADComposition =  ExecutePLMProcedure("XCAD_CompleteItems", PLMIDSetRestricted)
PLMIDSetRestricted = PLMIDSetRestricted + Restrict(idsXCADComposition, "PRODUCTCFG/VPMReference")

/*                              */
/* Enrich Rep with XCAD Non PS  */ 
/*                              */ 
idsXCADNonPS =  ExecutePLMProcedure("XCAD_CompleteNonPS", PLMIDSetRestricted)


/*Completes with all VPM Reference/Instance children */
PLMRouteSet1 = ExecutePLMFunction("PRODUCTCFG", "ProductCfg_AddChildrenProduct", PLMIDSetRestricted)
PLMIDSet1 = iPLMIDSet + Ids(PLMRouteSet1) /* PLMIDSet1 for expand reps*/

/*Completes with all VPM Representations(except NavRep), VPMPort and VPMConnection*/
PLMIDSet1Restricted = Restrict(PLMIDSet1, "PRODUCTCFG/VPMReference")
PLMRouteSet2 = ExecutePLMFunction("PRODUCTCFG", "ProductCfg_AddVPMRepsPortsAndConnections", PLMIDSet1Restricted)
PLMIDSet2 = Ids(PLMRouteSet2)

/*Completes with Ports on Representations*/
PLMIDSetRep = iPLMIDSet + Restrict(PLMIDSet2, "PRODUCTCFG/VPMRepReference")
PLMRouteSet3 = ExecutePLMFunction("PRODUCTCFG", "ProductCfg_AddVPMPorts", PLMIDSetRep)

/* Completes with XCADRepRepInsts */
rsXCADRepRepInst = ExecutePLMFunction("XCADAssembly", "XcadAssembly_ExpandVPMRefToXCADRepRepInst", PLMIDSetRestricted)


/*Completes with Engineering Connections */
PLMRouteSet4 = ExecutePLMFunction("CATMCXAssembly", "CATMCXAssembly_AddAllAggregatedMCX", PLMIDSet1Restricted)


oPLMIDSet = PLMIDSet1 + PLMIDSet2 + Ids(rsXCADRepRepInst) + Ids(PLMRouteSet3)+ Ids(PLMRouteSet4) + idsXCADNonPS + idsXCADComposition
}
